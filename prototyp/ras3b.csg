module ras3b() {
	r=3;
	$fn = 45;

	difference() {
		translate([r, r, -1])
			color([0, 0.5, 0])
				minkowski() {
					cube([85 - 2 * 3, 56 - 2 * r, 0.5]);
					cylinder(r=r, h=0.5);
				}
		for (p = [[0,0], [0,1], [1,0], [1,1]]) {
			translate([3.5 + p[0] * 58, 3.5 + p[1] * 49, - 2]) {
				cylinder(h = 3, r = 2.75 / 2);
				translate([0, 0, 1.99])
					color([0.99, 0.81, 0])
						cylinder(h = 1, r = 3);
			}
		}
	}
	color([1,1,1]) {
		translate([65.6, 10.25 - 15.5 / 2, 0]) 
			cube([21.4, 15.5, 13.5]);
		for (d = [29, 47])
			translate([69.25, d - 13.1 / 2, 0]) 
				union() {
					cube([17.75, 13.1, 16]);
					translate([17.75 - 0.5, - 0.75, 0]) {
						cube([0.5, 13.1 + 2 * 0.75, 16]);
					}
				}
	}

	translate([29 + 3.5 - 51.2 / 2, 56 - 1 - 5, 0]) {
		color([0, 0, 0])
			cube([51.2, 5, 2.5]);
		s = 0.64;
		d = 2.54;
		t = 2.5 - d / 2 - s / 2;
		for (x = [0:20-1])
			for (y = [0,1])
				translate([d * x + t, d * y + t, 8.5 - 11.3])
					color([1,1,1])
						cube([s, s, 11.3]);
	}

	module cam() {
		color([1, 0.9, 0.9])
			linear_extrude(height = 5.5)
				polygon([
					[0, - 22.5 / 2],
					[2.8, - 22.5 / 2],
					[2.8, - 15.6 / 2],
					[4, - 13.4 / 2],
					[4, + 13.4 / 2],
					[2.8, + 15.6 / 2],
					[2.8, 22.5 / 2],
					[0, 22.5 / 2],
				]);
	}
	translate([1.5 + 4, 28, 0])
		rotate([0, 0, 180])
			cam();
	translate([32 + 13 - 2.8 / 2, 22.5 / 2 + 0.3, 0])
			cam();

	translate([53.5 - 7 / 2, 0, 0])
		color([0, 0, 0])
			union() {
				cube([7, 12.5, 6]);
				translate([7 / 2, 1, 6 / 2])
					rotate([90, 0, 0])
						cylinder(r = 3, h=3.5);
			}

	translate([32 - 14.5 / 2, - 1.5, 0])
		color([1, 1, 1])
			cube([14.5, 12, 6.5]);

	translate([10.6 - 7.4 / 2, - 1.25, 0])
		color([1, 1, 1])
			union() {
				cube([7.4, 5.8, 3.25]);
				translate([- 0.3, 0, - 0.325])
					cube([8, 0.5, 3.9]);
			}

	translate([20, 24.5, 0])
		color([0, 0, 0])
			cube([14, 14, 0.5]);
}

// Don't include <raspi.csg>, use <raspi.csg>.
ras3b();
